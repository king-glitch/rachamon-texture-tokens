buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply plugin: 'net.minecraftforge.gradle'

version = modVersion
group = modGroup

minecraft {
    mappings channel: 'snapshot' as String, version: '20180814-1.12'
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge-repo'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        url 'https://jitpack.io'
    }
    ivy {
        name = 'Pixelmon Reforged'
        url "https://download.nodecdn.net/containers/reforged/server/release"
        metadataSources {
            artifact()
        }
        patternLayout {
            artifact "[revision]/[artifact].[ext]"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:' + forgeVersion
    compileOnly 'org.spongepowered:spongeapi:' + spongeVersion
    compileOnly 'pixelmon:Pixelmon-1.12.2-' + pixelmonVersion + '-server:' + pixelmonVersion
    shadow 'com.github.king-glitch:sponge-plugin-api:f7e9720f84'
}


shadowJar {
    configurations = [project.configurations.shadow]
    classifier ''
}

reobf {
    shadowJar {

    }
}

processResources {
    filesMatching("mcmod.info") {
        expand(
                'version': project.modVersion,
                'mcversion': project.mcVersion
        )
    }
}

